package com.ericsson.xtumlrt.oopl.cppmodel.derived

import "http://www.eclipse.org/emf/2002/Ecore"

import "http://www.ericsson.com/xtumlrt/oopl/cpp"
import "http://www.ericsson.com/xtumlrt/oopl"
import "http://www.eclipse.org/papyrusrt/xtumlrt/common"

@QueryBasedFeature(feature = "type")
pattern cppAttributeType(
	source : CPPAttribute,
	target : OOPLType
) {
	find cppAttributeUnnamedSequenceType(source, target);
} or {
	CPPAttribute.commonAttribute.type(source, commonType);
	OOPLType.commonType(target, commonType);
	neg find cppAttributeUnnamedSequenceType(source, _sequence);
}

@QueryBasedFeature(feature = "type")
pattern cppFormalParameterType(
	source : CPPFormalParameter,
	target : OOPLType
) {
	find cppFormalParameterUnnamedSequenceType(source, target);
} or {
	CPPFormalParameter.commonParameter.type(source, commonType);
	OOPLType.commonType(target, commonType);
	neg find cppFormalParameterUnnamedSequenceType(source, _sequence);
}

@QueryBasedFeature(feature = "cppName")
pattern cppNamedElementCppName(
	source : CPPNamedElement,
	target : EString
) {
	CPPNamedElement.ooplNameProvider(source, provider);
	OOPLExistingNameProvider.commonNamedElement(provider, element);
	NamedElement.name(element, target);
} or {
	CPPNamedElement.ooplNameProvider(source, provider);
	OOPLDerivedNameProvider.name(provider, target);
}

@QueryBasedFeature(feature = "cppPrefix")
pattern cppQualifiedNamedElementCppPrefix(
	source : CPPQualifiedNamedElement,
	target
) {
	find cppQualifiedNamedElementParentChild(parent, source);
	find cppQualifiedNamedElementCppQualifiedName(parent, parentQN);
	target == eval(parentQN + "::");
} or {
	neg find cppQualifiedNamedElementParentChild(_, source);
	target == eval("::");
}

@QueryBasedFeature(feature = "cppQualifiedName")
pattern cppQualifiedNamedElementCppQualifiedName(
	source : CPPQualifiedNamedElement,
	target
) {
	find cppQualifiedNamedElementParentChild(parent, source);
	find cppQualifiedNamedElementCppQualifiedName(parent, parentQN);
	find cppNamedElementCppName(source, name);
	target == eval(parentQN + "::" + name);
} or {
	neg find cppQualifiedNamedElementParentChild(_, source);
	find cppNamedElementCppName(source, name);
	target == eval("::" + name);
}

pattern cppQualifiedNamedElementParentChild(
	parent : CPPQualifiedNamedElement,
	child : CPPQualifiedNamedElement
) {
	CPPQualifiedNamedElement.subElements(parent, child);
}

@QueryBasedFeature(feature = "name")
pattern cppDirectoryName(
	source : CPPDirectory,
	target
) {
	find cppNamedElementCppName(component, target);
	CPPComponent.headerDirectory(component, source);
} or {
	find cppNamedElementCppName(component, target);
	CPPComponent.bodyDirectory(component, source);
} or {
	find cppNamedElementCppName(component, target);
	CPPComponent.externalHeaderDirectory(component, source);
} or {
	find cppNamedElementCppName(component, target);
	CPPComponent.externalBodySkeletonDirectory(component, source);
} or {
	find cppNamedElementCppName(model, cppName);
	CPPModel.headerDir(model, source);
	target == eval("headers/" + cppName);
} or {
	find cppNamedElementCppName(model, cppName);
	CPPModel.bodyDir(model, source);
	target == eval("bodies/" + cppName);
} or {
	find cppNamedElementCppName(model, cppName);
	CPPModel.externalHeaderDir(model, source);
	target == eval("headers/" + cppName);
} or {
	find cppNamedElementCppName(model, cppName);
	CPPModel.externalBodySkeletonDir(model, source);
	target == eval("headers/" + cppName);
} or {
	find cppNamedElementCppName(pack, target);
	CPPPackage.headerDir(pack, source);
} or {
	find cppNamedElementCppName(pack, target);
	CPPPackage.bodyDir(pack, source);
}

@QueryBasedFeature(feature = "parentDirectory")
pattern cppDirectoryParentDirectory(
	source : CPPDirectory,
	target
) {
	find cppDirectoryParentChild(parent, source);
	CPPDirectory.name(parent, target);
} or {
	neg find cppDirectoryParentChild(parent, source);
	target == eval("");
}

@QueryBasedFeature(feature = "path")
pattern cppDirectoryPath(
	source : CPPDirectory,
	target
) {
	find cppDirectoryParentChild(parent, source);
	find cppDirectoryPath(parent, prefix);
	find cppDirectoryName(source, name);
	target == eval(prefix + "/" + name);
} or {
	neg find cppDirectoryParentChild(_, source);
	find cppDirectoryName(source, target);
}

pattern cppDirectoryParentChild(
	parent : CPPDirectory,
	child : CPPDirectory
) {
	CPPDirectory.subDirectories(parent, child);
}

@QueryBasedFeature(feature = "generationName")
pattern cppSourceFileGenerationName(
	source : CPPSourceFile,
	target
) {
	find cppNamedElementCppName(class, cppName);
	CPPClass.headerFile(class, source);
	find cppSourceFileExtension(source, ext);
	target == eval(cppName + ext);
} or {
	find cppNamedElementCppName(class, cppName);
	CPPClass.bodyFile(class, source);
	find cppSourceFileExtension(source, ext);
	target == eval(cppName + ext);	
} or {
	find cppNamedElementCppName(component, cppName);
	CPPComponent.mainHeaderFile(component, source);
	find cppSourceFileExtension(source, ext);
	target == eval(cppName + "_main" + ext);
} or {
	find cppNamedElementCppName(component, cppName);
	CPPComponent.declarationHeaderFile(component, source);
	find cppSourceFileExtension(source, ext);
	target == eval(cppName + "_decl" + ext);
} or {
	find cppNamedElementCppName(component, cppName);
	CPPComponent.definitionHeaderFile(component, source);
	find cppSourceFileExtension(source, ext);
	target == eval(cppName +"_def" + ext);
} or {
	find cppNamedElementCppName(component, cppName);
	CPPComponent.mainBodyFile(component, source);
	find cppSourceFileExtension(source, ext);
	target == eval(cppName+ "_main" + ext);
} or {
	find cppNamedElementCppName(model, cppName);
	CPPModel.declarationHeaderFile(model, source);
	find cppSourceFileExtension(source, ext);
	target == eval(cppName + "_decl" + ext);
} or {
	find cppNamedElementCppName(model, cppName);
	CPPModel.definitionHeaderFile(model, source);
	find cppSourceFileExtension(source, ext);
	target == eval(cppName + "_def" + ext);
} or {
	find cppNamedElementCppName(model, cppName);
	CPPModel.apiHeaderFile(model, source);
	find cppSourceFileExtension(source, ext);
	target == eval(cppName + "_api" + ext);
} or {
	find cppNamedElementCppName(pack, cppName);
	CPPPackage.headerFile(pack, source);
	find cppSourceFileExtension(source, ext);
	target == eval(cppName + ext);
} or {
	find cppNamedElementCppName(pack, cppName);
	CPPPackage.bodyFile(pack, source);
	find cppSourceFileExtension(source, ext);
	target == eval(cppName + ext);
} or {
	find cppNamedElementCppName(port, cppName);
	CPPPort.headerFile(port, source);
	find cppSourceFileExtension(source, ext);
	target == eval(cppName + ext);
} or {
	find cppNamedElementCppName(port, cppName);
	CPPPort.externalHeaderFile(port, source);
	find cppSourceFileExtension(source, ext);
	target == eval(cppName + "_impl" + ext);
} or {
	find cppNamedElementCppName(port, cppName);
	CPPPort.bodyFile(port, source);
	find cppSourceFileExtension(source, ext);
	target == eval(cppName + ext);
} or {
	find cppNamedElementCppName(port, cppName);
	CPPPort.externalBodySkeletonFile(port, source);
	find cppSourceFileExtension(source, ext);
	target == eval(cppName + "_impl" + ext + "_skel");
} or {
	find cppNamedElementCppName(protocol, cppName);
	CPPProtocol.headerFile(protocol, source);
	find cppSourceFileExtension(source, ext);
	target == eval(cppName + ext);
}

pattern cppSourceFileExtension(
	source : CPPSourceFile,
	ext
) {
	CPPHeaderFile(source);
	ext == eval(".hh");
} or {
	CPPBodyFile(source);
	ext == eval(".cc");
}

@QueryBasedFeature(feature = "generationDirectory")
pattern cppSourceFileGenerationDirectory(
	source : CPPSourceFile,
	target
) {
	CPPDirectory.files(directory, source);
	find cppDirectoryPath(directory, target);
}

@QueryBasedFeature(feature = "generationPath")
pattern cppSourceFileGenerationPath(
	source : CPPSourceFile,
	target
) {
	find cppSourceFileGenerationDirectory(source, includeDirectory);
	find cppSourceFileGenerationName(source, includeName);
	target == eval(includeDirectory + "/" + includeName);
}

@QueryBasedFeature(feature = "includeName")
pattern cppHeaderFileIncludeName(
	source : CPPHeaderFile,
	target
) {
	find cppSourceFileGenerationName(source, target);
}

@QueryBasedFeature(feature = "includeDirectory")
pattern cppHeaderFileIncludeDirectory(
	source : CPPHeaderFile,
	target
) {
	find cppSourceFileGenerationDirectory(source, target);
}

@QueryBasedFeature(feature = "includePath")
pattern cppHeaderFileIncludePath(
	source : CPPHeaderFile,
	target
) {
	find cppSourceFileGenerationPath(source, target);
}

@QueryBasedFeature(feature = "cppContainer")
pattern cppClassRefSimpleCollectionCppContainer(
	source : CPPClassRefSimpleCollection,
	target
) {
	CPPClassRefSimpleCollection.kind(source, kind);
	kind == SimpleCollectionKind::INDEXED_ARRAY;
	target == eval("std::array");
} or {
	CPPClassRefSimpleCollection.kind(source, kind);
	kind == SimpleCollectionKind::SIMPLY_LINKED_LIST;
	find simplyLinkedListType(target);
} or {
	CPPClassRefSimpleCollection.kind(source, kind);
	kind == SimpleCollectionKind::DOUBLY_LINKED_LIST;
	target == eval("std::list");
} or {
	CPPClassRefSimpleCollection.kind(source, kind);
	kind == SimpleCollectionKind::DOUBLE_ENDED_QUEUE;
	target == eval("std::deque");
}

// this pattern is for selecting between C++03 and C++11 version of the list depending on the target platform
pattern simplyLinkedListType(
	cppContainer
) {
	// TODO: select between std::list and std::forward_list depending on platform
	cppContainer == eval("std::list");
}

pattern cppSequenceType(
	cppSequence : CPPSequence,
	ooplType : OOPLType
) {
	CPPSequence.elementType(cppSequence, ooplType);
}

pattern cppAttributeUnnamedSequenceType(
	cppAttribute : CPPAttribute,
	cppSequence : CPPSequence
) {
	CPPAttribute.unnamedSequenceType(cppAttribute, cppSequence);
}

pattern cppFormalParameterUnnamedSequenceType(
	cppFormalParameter : CPPFormalParameter,
	cppSequence : CPPSequence
) {
	CPPFormalParameter.unnamedSequenceType(cppFormalParameter, cppSequence);
}

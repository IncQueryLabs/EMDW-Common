package com.incquerylabs.emdw.umlintegration.queries

import "http://www.eclipse.org/uml2/5.0.0/UML"

pattern xtComponent(umlPackage: Package, component: Component) {
	Package.packagedElement(umlPackage, component);
}

pattern xtClass(umlPackage: Package, umlClass: Class) {
	Package.packagedElement(umlPackage, umlClass);
	neg find xtComponent(umlPackage, umlClass); // In UML, a Component is also a Class!
}

// Events

pattern xtClassEvent(umlClass: Class, signal: Signal) {
	Class.nestedClassifier(umlClass, signal);
}

pattern xtSignalEvent(umlClass: Class, signal: Signal) {
	Interface.nestedClassifier(_, signal);

	Class.classifierBehavior(umlClass, stateMachine);
	find transitionInStateMachine(stateMachine, transition);
	Transition.trigger(transition, trigger);
	Trigger.event(trigger, signalEvent);
	SignalEvent.signal(signalEvent, signal);
}

private pattern transitionInStateMachine(stateMachine: StateMachine, transition: Transition) {
	StateMachine.region(stateMachine, toplevelRegion);
	Region.transition(toplevelRegion, transition);
} or {
	StateMachine.region(stateMachine, toplevelRegion);
	find subregion+(toplevelRegion, region);
	Region.transition(region, transition);
}

private pattern subregion(parent: Region, child: Region) {
	Region.subvertex(parent, state);
	State.region(state, child);
}